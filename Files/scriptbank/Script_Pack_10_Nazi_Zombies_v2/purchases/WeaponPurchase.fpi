; Script by HuKuTa94
; !!! DONT USE THIS SCRIPT IN ANY FIELD !!!

; non-editable vars (dont change these vars!):
; cost					// cost of weapon 
; nextState			// it is using for splitting of the script logic 
; isHudShowed		// 0 - cost hud is hidden; 1 - cost hud is showed 

; dimlocalvar=cost can take the following values: (cause of VARGREATER=X fpi command, it means VAR>X but, not VAR>=X)
; 249, 499, 749, 949, 1199, 1499, 1999


;==============================================================================

;// non-editable vars
:state=0:dimlocalvar=nextState,dimlocalvar=isHudShowed,setvar=isHudShowed 0

;// run main logic
:state=0:floorlogic,coloff,setbulletcoloff=1,state=1


;==============================================================================

;// show hud
:state=1,plrdistwithin=50,plrfacing=40:setvar=nextState 2,state=%cost

;// hide hud
:state=2,plrnotfacing=40:state=20
:state=2,plrdistfurther=50:state=20
:state=20:setvar=nextState 1,state=%cost


;==============================================================================

;// weapon purchase
:state=2,plrdistwithin=50,keypressed=33 1,vargreater=money %cost:state=3
:state=3,plrdistfurther=50:state=20
:state=3,plrnotfacing=40:state=20
:state=3,keypressed=33 0:removecurrentweapon,setvar=nextState 4,etimerstart,subvar=money %cost,subvar=money 1,plrsound=audiobank\Script_Pack_10_Nazi_Zombies_v2\purchase_buy.ogg,state=%cost
:state=4,etimergreater=600:playertake

;// not enough of money
:state=2,plrdistwithin=50,keypressed=33 1,varless=money %cost:state=5
:state=5,plrnotfacing=40:state=20
:state=5,plrdistfurther=50:state=20
:state=5,plrdistwithin=50,keypressed=33 0:state=2,plrsound=audiobank\Script_Pack_10_Nazi_Zombies_v2\purchase_no_money.ogg


;==============================================================================

;// show/hide specific cost hud
:state=249,varequal=isHudShowed 0:state=10,hudshow=weapon250
:state=249,varequal=isHudShowed 1:state=10,hudunshow=weapon250
:state=499,varequal=isHudShowed 0:state=10,hudshow=weapon500
:state=499,varequal=isHudShowed 1:state=10,hudunshow=weapon500
:state=749,varequal=isHudShowed 0:state=10,hudshow=weapon750
:state=749,varequal=isHudShowed 1:state=10,hudunshow=weapon750
:state=949,varequal=isHudShowed 0:state=10,hudshow=weapon950
:state=949,varequal=isHudShowed 1:state=10,hudunshow=weapon950
:state=1199,varequal=isHudShowed 0:state=10,hudshow=weapon1200
:state=1199,varequal=isHudShowed 1:state=10,hudunshow=weapon1200
:state=1499,varequal=isHudShowed 0:state=10,hudshow=weapon1500
:state=1499,varequal=isHudShowed 1:state=10,hudunshow=weapon1500
:state=1999,varequal=isHudShowed 0:state=10,hudshow=weapon2000
:state=1999,varequal=isHudShowed 1:state=10,hudunshow=weapon2000

;// change flag
:state=10,varequal=isHudShowed 0:setvar=isHudShowed 1,state=%nextState
:state=10,varequal=isHudShowed 1:setvar=isHudShowed 0,state=%nextState
