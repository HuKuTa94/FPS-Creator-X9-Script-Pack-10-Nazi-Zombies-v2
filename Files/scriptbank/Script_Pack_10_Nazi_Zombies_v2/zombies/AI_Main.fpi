; Script by HuKuTa94
; use this script in ai/main field

;// money for hits
:shotdamage=1:addvar=money 10

;// remove from game if player is far
:plrdistfurther=1000:airemove,subvar=zombiesInGame 1,addvar=zombiesCountInCurrentWave 1,destroy


;========================================================================

;// init
:state=0:addaiteam=1
:state=0,varequal=animMove %animRun:aisetspeed=180
:state=0,varequal=animMove %animWalk:aisetspeed=100
:state=0:setaiactive=0,animate=%animMove,setanimatespeed=110,waypointstart,state=1


;========================================================================

;// waypoint
;// reached waypoint node
:state=1,waypointstate=3:state=2
:state=1,waypointstate=5:state=2

;// go to next waypoint node
:state=2,waypointstate=3:waypointnext,state=1
:state=2,waypointstate=4:waypointrandom,state=1

;// reached the alive barricade
:state=2,waypointstate=5,nearactivatable=2:waypointstop,state=4

;// reached the broken barricade
:state=2,waypointstate=5,nearactivatable=3:waypointstop,state=10

;// no barricade
:state=2,waypointstate=5:waypointstop,state=10


;========================================================================

;// zombie begins to break the barricade
:state=4,nearactivatable=2:state=5,sound=$1,alwaysactive=1,aisetspeed=50,settarget,rotatetotarget

;// choose random animation
:state=5,random=10:setvar=currentAnim %animAttack1,setvar=currentAnimBeyond %animAttack1Beyond,state=6
:state=5,random=10:setvar=currentAnim %animAttack2,setvar=currentAnimBeyond %animAttack2Beyond,state=6
:state=5,random=10:setvar=currentAnim %animAttack3,setvar=currentAnimBeyond %animAttack3Beyond,state=6
:state=5,random=10:setvar=currentAnim %animAttack4,setvar=currentAnimBeyond %animAttack4Beyond,state=6


;// attack animation begins
:state=6:setframe=%currentAnim,state=7
:state=7:incframe=%currentAnim
:state=7,framebeyond=%currentAnim %currentAnimBeyond:state=8

;// damage event
:state=8,varequal=isBoss 0,nearactivatable=2:settarget,activatetarget=1
:state=8,varequal=isBoss 1,nearactivatable=2:settarget,activatetarget=10
:state=8,plrdistwithin=60:plrsubhealth=%damage
:state=8:state=9

;// attack animation ends
:state=9:incframe=%currentAnim
:state=9,frameatend=%currentAnim:state=4


;// barricade has been broken
:state=4,nearactivatable=3:state=10
:state=7,nearactivatable=3:state=10


;==============================================================================

;// entering in player's area
;// jump
:state=10:aisetspeed=100,etimerstart,state=11
:state=11:animate=%animJump,runfore=4
:state=11,etimergreater=600:setframe=%animLanding,state=12
:state=12:incframe=%animLanding
:state=12,frameatend=%animLanding:airemove,state=20


;===========================================================================================================================

;// run attack script
:state=20:runfpi=Script_Pack_10_Nazi_Zombies_v2\zombies\Attack.fpi
